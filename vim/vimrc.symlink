filetype on   " Ensure that the following line doesn't cause an error.
filetype off  " Ensure that pathogen is around when we turn it back on.
call pathogen#runtime_append_all_bundles()
call pathogen#helptags()

syntax enable
colorscheme sfiera

set nocompatible
set backspace=2
set showcmd
set ruler

filetype plugin indent on
set tabstop=4
set softtabstop=0
set shiftwidth=4
set nosmarttab
set expandtab
set autoindent
set cindent
set cinoptions=g2,:2,=2,l1,h2
set textwidth=99
set modeline
set modelines=5

set encoding=utf-8
set termencoding=utf-8

set wildmenu
set wildmode=list:longest,full

noremap ; :

" Correct some "incorrect" mappings
nmap Y y$
vmap Y yy
nmap S ch
nmap cw dwi
nmap cW dWi
nmap U <C-R>

noremap H :call NewH()<CR>
noremap J :call NewJ()<CR>
noremap K :call NewK()<CR>
noremap L :call NewL()<CR>

function! NewH()
    let l:col = getpos(".")[2]
    normal! ^
    if getpos(".")[2] >= l:col
        normal! -
    endif
endfunction

function! NewJ()
    let l:lnum = getpos(".")[1]
    normal! L
    if getpos(".")[1] <= l:lnum
        normal! zzL
    endif
endfunction

function! NewK()
    let l:lnum = getpos(".")[1]
    normal! H
    if getpos(".")[1] >= l:lnum
        normal! zzH
    endif
endfunction

function! NewL()
    let l:col = getpos(".")[2]
    normal! $
    if getpos(".")[2] <= l:col
        normal! 2$
    endif
endfunction

" Alternate quickly through tabs.
nmap ` :tabnext<CR>
nmap ~ :tabprevious<CR>

onoremap <silent>af :<C-u>normal! ggVG$<CR>
omap <silent>if af
vnoremap <silent>af ggoG$
vmap <silent>if af

vnoremap <silent>m <ESC>:call VisualSearch('/')<CR>/<CR>
vnoremap <silent>M <ESC>:call VisualSearch('?')<CR>?<CR>

function! VisualSearch(dirrection)
    let l:register=@@
    normal! gvy
    let l:search=escape(@@, '$.*/\[]')
    if a:dirrection=='/'
        execute 'normal! /'.l:search
    else
        execute 'normal! ?'.l:search
    endif
    let @/=l:search
    normal! gV
    let @@=l:register
endfunction

command Q :qall
command W :wall
command Wq :wqall
command WQ :wqall

runtime sfiera/arrows.vim

command Trim :%s/ \+$//

let g:alternateRelativeFiles = 1
let g:SuperTabLeadingSpaceCompletion = 0
let g:alternateSearchPath = "reg:/src/include/g,reg:/include/src/g,reg:/inc/src/g,reg:/src/inc/g"
