#!/bin/zsh

set -o errexit
set -o nounset

function @ {
    echo "$@"
    $@
}

if ! [[ -d ~/.dotfiles ]]; then
    git clone https://github.com/sfiera/.dotfiles ~/.dotfiles
fi
cd ~/.dotfiles

if [[ "${SHELL##*/}" != zsh ]]; then
    chsh -s "$(which zsh)"
fi

DEFS=~/.defs.sh
if [[ ! -f $DEFS ]]; then
    case $(uname) in
        Darwin) DEFAULT_OS=osx DEFAULT_FORM=laptop ;;
        Linux) DEFAULT_OS=linux DEFAULT_FORM=server ;;
    esac
    case $(hostname) in
        *.corp.*) DEFAULT_ENV=corp ;;
        *) DEFAULT_ENV=home ;;
    esac

    echo -n "os [$DEFAULT_OS]: "
    read OS
    echo OS=${OS:-$DEFAULT_OS} >> ~/.defs.sh
    echo -n "env [$DEFAULT_ENV]: "
    read ENV
    echo ENV=${ENV:-$DEFAULT_ENV} >> ~/.defs.sh
    echo -n "form [$DEFAULT_FORM]: "
    read FORM
    echo FORM=${FORM:-$DEFAULT_FORM} >> ~/.defs.sh
fi
source $DEFS

is_osx()      { [[ $OS == osx ]] }
is_linux()    { [[ $OS == linux ]] }
is_home()     { [[ $ENV == home ]] }
is_corp()     { [[ $ENV == corp ]] }
is_laptop()   { [[ $FORM == laptop ]] }
is_desktop()  { [[ $FORM == desktop ]] }
is_server()   { [[ $FORM == server ]] }

for prop in osx linux home corp laptop desktop server; do
    if is_$prop; then
        alias if_$prop= if_not_$prop=true
    else
        alias if_$prop=true if_not_$prop=
    fi
done

export ZSH=$(dirname $0)
git submodule update --init --recursive
for config_file ($ZSH/**/*.install) source $config_file
