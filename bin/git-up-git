#!/bin/zsh

set -o errexit
set -o nounset

if ! REMOTE=$(git config --get branch.master.remote); then
    echo >&2 "master has no remote"
    exit 1
fi

SOURCE=refs/heads/master
TARGET=refs/remotes/$REMOTE/master

git fetch $REMOTE
if git merge-base --is-ancestor $TARGET $SOURCE; then
    if git merge-base --is-ancestor $SOURCE $TARGET; then
        echo >&2 "master is up-to-date"
    else
        echo >&2 "master is ahead of $REMOTE/master"
    fi
elif git merge-base --is-ancestor $SOURCE $TARGET; then
    FROM=$(git show-ref $SOURCE -s)
    TO=$(git show-ref $TARGET -s)

    echo >&2 "updating master to $REMOTE/master ${FROM[1,7]}..${TO[1,7]}"
    if [[ $(git symbolic-ref HEAD) == $SOURCE ]]; then
        git merge --ff-only --no-stat --quiet $TARGET
    else
        git update-ref $SOURCE $TARGET
    fi
    git diff --stat $FROM $TO || true
else
    echo >&2 "master and $REMOTE/master have diverged"
    exit 1
fi
